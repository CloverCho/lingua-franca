target TypeScript {
    timeout : 5 sec
};
main reactor SlowingClock {
    logical action a(100 msec);
    state interval:time(100 msec);
    state expected_time:time(100 msec);
    reaction(startup) -> a {=
        actions.a.schedule(0, null);
    =}
    reaction(a) -> a {=
        let elapsed_logical_time : TimeValue = util.getElapsedLogicalTime();
        console.log("Logical time since start: " + elapsed_logical_time);
        if (!elapsed_logical_time.isEqualTo(expected_time)) {
            console.log("ERROR: Expected time to be: " + expected_time);
            util.failure();
            //throw new Error("ERROR: Expected time to be: " + (this.state as any).expected_time);
        }
        actions.a.schedule(interval, null);
        expected_time = expected_time.add(new UnitBasedTimeValue(100 , TimeUnit.msec))
        expected_time = expected_time.add(interval);
        interval = interval.add(new UnitBasedTimeValue(100 , TimeUnit.msec));
    =}
    reaction(shutdown) {=
        if (!expected_time.isEqualTo(new UnitBasedTimeValue(5500 , TimeUnit.msec))) {
            console.log("ERROR: Expected the next expected time to be: " + new UnitBasedTimeValue(5500 , TimeUnit.msec));
            console.log("It was: " + expected_time);
            util.failure();
            //throw new Error("ERROR: Expected the next expected time to be: [ 5, 500000000 ]");
        }
    =}
}