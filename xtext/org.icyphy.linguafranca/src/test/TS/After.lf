// This checks that the after keyword adjusts logical time, not
// using physical time.
target TypeScript {
    fast: false,
    timeout: 3 sec
};
reactor foo {
    input x:number;
    output y:number;
    reaction(x) -> y {=
        y.set(2*(x as number));
    =}
}
reactor print {
    state expected_time:time(10 msec);
    input x:number;
    reaction(x) {=
        let elapsed_time = util.time.getElapsedLogicalTime();
        console.log("Result is " + x);
        console.log("Current logical time is: " + elapsed_time);
        console.log("Current physical time is: " + util.time.getElapsedPhysicalTime());
        if (! elapsed_time.isEqualTo(expected_time)) {
            console.log("ERROR: Expected logical time to be " + expected_time);
            util.exec.failure();
        }
        expected_time = expected_time.add(new UnitBasedTimeValue(1, TimeUnit.sec));
    =}
}
main reactor top {
    f = new foo();
    p = new print();
    timer t(0, 1 sec);
    reaction(t) -> f.x {=
        f.x.set(42);
    =}
    f.y -> p.x after 10 msec;
}