// This tests the mechanism for testing whether a previous reaction has
// produced a given output. The output should always be 42.
target C;
reactor Source {
    output out:int;
    reaction(startup) -> out {=
        // Set a seed for random number generation based on the current time.
        srand(time(0));
        // Randomly produce an output or not.
        if (rand() % 2) {
            set(out, 21);
        }
    =}
    reaction(startup) -> out {=
        if (out_is_present) {
            int previous_output = *out;
            set(out, 2 * previous_output);
        } else {
            set(out, 42);
        }
    =}
}
reactor Sink {
    input in:int;
    reaction(in) {=
        printf("Received %d.\n", in);
        if (in != 42) {
            fprintf(stderr, "FAILED: Expected 42.\n");
            exit(1);
        }
    =}
}
main reactor TestForPreviousOutput {
    s = new Source();
    d = new Sink();
    s.out -> d.in;
}