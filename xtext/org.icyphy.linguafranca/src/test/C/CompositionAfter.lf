// This test connects a simple counting source to tester
// that checks against its own count.
target C(
	run = "bin/Composition -timeout 10 sec"
);
reactor Source(period:time(2 sec)) {
	output y:int;
	timer t(1 sec, period);
	state count:int(0);
	reaction(t) -> y {=
		(self->count)++;
		set(y, self->count);
	=}
}

reactor Test {
	input x:int;
	state count:int(0);
	reaction(x) {=
		(self->count)++;
		printf("Received %d\n", x);
		if (x != self->count) { 
			printf("FAILURE: Expected %d\n", self->count);
			exit(1); 
		}
	=} 
}
main reactor Composition {
	s = new Source();
	
	d = new Test();
	s.y -> d.x after 1 sec;
}