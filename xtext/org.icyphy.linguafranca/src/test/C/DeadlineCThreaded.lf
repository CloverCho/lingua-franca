// This test checks that a deadline violation is detected.
// It will exit with error code 2 if the deadline violation is not detected.
target C(
	threads = 1,
	run = "bin/DeadlineC -timeout 2 sec"
);
reactor Source(period:time(1 sec)) {
	output y:int;
	timer t(0, period);
	state count:int(0);
	reaction(t) -> y {=
		(self->count)++;
        // Take time to cause a deadline violation.
		struct timespec sleep_time = {(time_t) 0, (long)200000000};
		struct timespec remaining_time;
		nanosleep(&sleep_time, &remaining_time);
		set(y, self->count);
	=}
} 
reactor Destination {
	input x:int;
	input e:int;
	reaction(x, e) {=
		if (e_is_present) {
			printf("**** Handle deadline miss.\n");
		} else {
			printf("FAILURE: Deadline miss was not detected.\n");
			exit(2);
		}
        // This reaction is triggered by the deadline handler below, so the input
        // x should also be present. It is logically simultaneous with the event e
        // generated by the deadline handler.
        if (x_is_present) {
            printf("x input: %d\n", x);
        } else {
            printf("FAILURE: Input that triggered the deadline should have been present but is not.\n");
            exit(3);            
        }
	=}
}
main reactor DeadlineCThreaded {
	logical action panic;
	reaction(panic) -> d.e {=
		printf("**** Deadline miss detected.\n");
		set(d.e, 1);
	=}
	s = new Source(period=1 sec);
	d = new Destination();
	s.y -> d.x; 
	deadline(d.x, 100 msec, panic);
}