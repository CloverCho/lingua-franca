// Example in the Wiki.
    target C (threads = 1);
    reactor Scale(scale:int(2)) {
        input x:int;
        output y:int;
        reaction(x) -> y {=
            set(y, x * self->scale);
        =}
    }
    reactor Test {
        input x:int;
        state received_value:bool(false);
        reaction(x) {=
            printf("Received %d.\n", x);
            self->received_value = true;
            if (x != 2) {
                printf("ERROR: Expected 2!\n");
                exit(1);
            }
        =}
        reaction(shutdown) {=
            if (!self->received_value) {
                printf("ERROR: No value received by Test reactor!\n");
            } else {
                printf("Test passes.\n");
            }
        =}
    }
    main reactor Gain {
        g = new Scale();
        d = new Test();
        g.y -> d.x;
        reaction(startup) -> g.x {=
            set(g.x, 1);
        =}
    }