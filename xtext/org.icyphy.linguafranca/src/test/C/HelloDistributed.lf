target C { 
    keepalive: true, // FIXME: Should be automatic.
    timeout: 3 sec,
    federates: {
        RTI: {host: "localhost", port: 15045, launcher: true},
        Source: {reactors: [s]},
        Destination: {reactors: [d]}
    }
};
reactor Source {
    output out:string;
    reaction(startup) -> out {=
        printf("Sending 'Hello World!' message from source federate.\n");
        set(out, "Hello World!");
    =}
} 
reactor Destination {
    input in:string;
    reaction(startup) {=
        printf("Destination started.\n");
    =}
    reaction(in) {=
        printf("Destination received: %s\n", in);
        if (strcmp(in, "Hello World!") != 0) {
            printf("ERROR: Expected to receive 'Hello World!'\n");
            exit(1);
        }
    =}
}
main reactor HelloDistributed {
    s = new Source();      // Reactor s is in federate Source
    d = new Destination(); // Reactor d is in federate Destination
    s.out -> d.in;
}