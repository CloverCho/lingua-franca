// Grammar for Lingua Franca
// The Lingua-Franca toolkit is is licensed under the BSD 2-Clause License.
// See LICENSE.md file in the top repository directory.

grammar org.icyphy.LinguaFranca with org.eclipse.xtext.common.Terminals

generate linguaFranca "http://www.icyphy.org/LinguaFranca"

/////////// Overall file
Model:
	target=Target 
	(imports += Import)*
	(reactors += Reactor)+
	;

/////////// Top level elements
Target:
	'target' name=ID ( '(' parameters=Assignments? ')' )? ';';
	
Import:
	'import' name=Path ';';

Reactor:
   	'reactor' name=ID parameters=Params? '{'
   	(preamble = Preamble)?
	((states += State)
   	| (inputs += Input)
   	| (outputs += Output)
   	| (timers += Timer)
   	| (actions += Action)
   	| (instances += Instance)
   	| (connections += Connection)
   	| (deadlines += Deadline)
   	| (reactions += Reaction))*
   	'}';
	
/////////// Statements

// Note explicit allowance of keywords for names of inputs, outputs, timers.
// Types are optional for use in untyped languages.
State:
	'state' name=ValidID (':' type=Type)? ('(' value=Value ')') ';';

Input:
	'input' name=ValidID (':' type=Type)? ';';

Output:
	'output' name=ValidID (':' type=Type)? ';';

Timer:
	'timer' name=ValidID timing=Timing?';';

Action:
	modifier='logical'? 'action' name=ValidID ('(' delay=(ID | NUMBER) ')')? (':' type=Type)? ';';

Reaction:
	'reaction' 
	('(' (triggers+=ValidID (',' triggers+=ValidID)*)? ')')?
	uses=Uses?
	produces=Produces?
	code=CODE;
	
Preamble:
	'preamble' code=CODE;
	
// FIXME: reactorClass should ideally be something like [Reactor|Composite],
// to get cross references to work. But this doesn't work for importing
// foreign reactors that are not defined in Lingua Franca.
Instance:
	name=ID '=' 'new' reactorClass=[Reactor] ('(' parameters=Assignments? ')')? ';' ;

Connection:
    leftPort=Port '->' rightPort=Port ';';
    
Deadline:
	'deadline' '(' port=Port ',' delay=Time ',' action=ValidID ')' ';';
    
ValidID:
	ID | KEYWORD;

KEYWORD:
	'input' | 'state' | 'output' | 'timer' | 'action';

/////////// Pieces

Assignments:
	assignments+=Assignment (',' assignments+=Assignment)*;
	
Assignment:
	name=ID '=' value=Value (unit=ID)?;

// Specification of what inputs a reaction reads.
Uses:
	uses+=ID (',' uses+=ID)?;

// Parameters for actors and composites.
Params:
    '(' params+=Param (',' params+=Param)* ')';

// Single parameter.
Param:
    'const'? name=ID ((':' type='time' '(' time=Time ')')
    		| (':' type=Type)? ('(' value=Value ')')
    		)?;

// Timing specification for a timer: (offset, period)
// Can be empty, which means (0,0) = (NOW, ONCE).
// E.g. (0) or (NOW) or (NOW, ONCE) or (100, 1000)
// The latter means fire with period 1000, offset 100.
Timing:
	'(' offset=Time (',' period=Time)?')';

// Time is either an ID (a reference to a parameter) or
// a constant, a number followed by a unit specification.
// Time unit here is any ID. The validator should check for valid units.
Time:
	time=ID | time=NUMBER (unit=ID)?;

// 'input' and 'output' are allowed port names despite being keywords.
Port:
    ValidID | (ValidID '.' ValidID);

// Specification of what outputs a reaction writes.
// Note that Port is either a ValidID or ValidID.ValidID
Produces:
	'->' produces+=Port (',' produces+=Port)?;

// A type is in the target language, hence either an ID or target code.
Type:
    ID | CODE ;
    
Value:
    ID | NUMBER | STRING | CODE;

/////////// Elementary components
// Terminals must be mutually exclusive. They are used by the lexer before parsing.

// FIXME: Can we replace this with something more general? E.g., scientific notation, hex, etc.?
terminal NUMBER:
    ('-'? INT ('.' INT)?) ;

// FIXME: What if the code needs to contain '=}'?
// Just escaping with \ is not a good idea because then every \ has to be escaped \\.
// Perhaps the string EQUALS_BRACE could become '=}'?
terminal CODE:
    '{=' .* '=}' ;

// The following cannot be terminal because it overlaps ID.
Path:
	ID ('.' ID)* ;
