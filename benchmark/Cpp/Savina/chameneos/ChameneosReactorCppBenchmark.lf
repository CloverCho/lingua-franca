/**
 * Micro-benchmark from the Savina benchmark suite. Intended
 * to measure the effects on shared resources (the mailbox of
 * the mall actor) while processing messages in the actor model.
 * See https://shamsimam.github.io/papers/2014-agere-savina.pdf.
 * 
 * To break the causality loop the mall reactor contains a logical
 * action.
 * 
 * Informal results for 2,000,000 meetings
 * on my PC with fast forward enabled:
 * Unthreaded: 1400 ms
 * Threaded: >17 s
 * 
 * For comparison some informal results for 2,000,000 meetings
 * and 6 Chameneos
 * on the same PC with the Savina Akka implementation:
 * Threaded: 1532 ms
 * 
 * @author Hannes Klein
 */

target Cpp {
    cmake-include: "ChameneosReactorCpp.cmake"
};

import BenchmarkRunner from "../BenchmarkRunner.lf";


reactor ChameneosMallReactor(numMeetings:int(200000), numChameneos:int(100)) {
    
    public preamble {=
        #include "ChameneosCommon.hh"
        #include "reactor-cpp/logging.hh"
    =}
    
    state sumMeetings:int(0);
    state numFaded:int(0);
    state meetings:int(numMeetings);
    state messages:{=std::vector<reactor::ImmutableValuePtr<Message>>=};
    
    state inputs:{=std::vector<reactor::Input<Message>*>=};
    state outputs:{=std::vector<reactor::Output<Message>*>=};
    
    input inStart:void;
    output outFinished:void;
    
    //TODO parametrize
    output outChameneo0:{=Message=};
    output outChameneo1:{=Message=};
    output outChameneo2:{=Message=};
    output outChameneo3:{=Message=};
    output outChameneo4:{=Message=};
    output outChameneo5:{=Message=};
    
    //TODO parametrize
    input inChameneo0:{=Message=};
    input inChameneo1:{=Message=};
    input inChameneo2:{=Message=};
    input inChameneo3:{=Message=};
    input inChameneo4:{=Message=};
    input inChameneo5:{=Message=};
    
    // logical action to break causality loop
    logical action pairChameneos:void;
    
    reaction(startup) {=
        
        // initializations
        messages = std::vector<reactor::ImmutableValuePtr<Message>>(numChameneos);
        
        // organize inputs and outputs in vectors for easier access
        
        //TODO parametrize
        inputs.reserve(numChameneos);
        inputs.push_back(&inChameneo0);
        inputs.push_back(&inChameneo1);
        inputs.push_back(&inChameneo2);
        inputs.push_back(&inChameneo3);
        inputs.push_back(&inChameneo4);
        inputs.push_back(&inChameneo5);
        
        //TODO parametrize
        outputs.reserve(numChameneos);
        outputs.push_back(&outChameneo0);
        outputs.push_back(&outChameneo1);
        outputs.push_back(&outChameneo2);
        outputs.push_back(&outChameneo3);
        outputs.push_back(&outChameneo4);
        outputs.push_back(&outChameneo5);
    =}
    
    //TODO parametrize
    reaction(inStart) ->
      outChameneo0,
      outChameneo1,
      outChameneo2,
      outChameneo3,
      outChameneo4,
      outChameneo5
    {=
        
         // reset local state
         sumMeetings = 0;
         numFaded = 0;
         meetings = numMeetings;
         
         // start execution
         for(int i = 0; i < outputs.size(); i++) {
            outputs[i]->set(Message{StartMsg});
         }
    
    =}
    
    //TODO parametrize
    reaction(pairChameneos) ->
      outChameneo0,
      outChameneo1,
      outChameneo2,
      outChameneo3,
      outChameneo4,
      outChameneo5
    {=
        
        int waitingChameneoIndex = -1;
        
        for(int i = 0; i < outputs.size(); ++i) {
            if(messages[i] != nullptr) {
                // Chameneo i present in mall
                if(meetings > 0) {
                    if(waitingChameneoIndex == -1) {
                        waitingChameneoIndex = i;
                    } else {
                        meetings -= 1;
                        reactor::log::Debug() << "setting up meeting " << meetings << " between: " << waitingChameneoIndex << ", " << i;
                        outputs[waitingChameneoIndex]->set(messages[i]);
                        messages[waitingChameneoIndex] = nullptr;
                        messages[i] = nullptr;
                        waitingChameneoIndex = -1;
                    }
               } else {
                    outputs[i]->set(Message{ExitMsg});
               }
            }
        }
    =}
    
    //TODO parametrize
    reaction(
        inChameneo0,
        inChameneo1,
        inChameneo2,
        inChameneo3,
        inChameneo4,
        inChameneo5
    ) -> outFinished {=
        
        // detect all chameneos that are present
        for(int i = 0; i < inputs.size(); ++i) {
            if(inputs[i]->is_present()) {
                if(inputs[i]->get()->type == MeetingCountMsg) {
                    numFaded += 1;
                    sumMeetings = sumMeetings + inputs[i]->get()->id; // reuse id field
                    if (numFaded == numChameneos) {
                        outFinished.set();
                        return;
                    }
                } else {
                    messages[i] = inputs[i]->get();
                }
            }
        }
        
        pairChameneos.schedule();
    =}
}

reactor ChameneosChameneoReactor(startColor:{=Color=}({=RED=}), id:int(-1), numChameneos:int(100)) {
    
    public preamble {=
        #include "ChameneosCommon.hh"
        #include "reactor-cpp/logging.hh"
    =}
    
    state meetings:int(0);
    state color:{=Color=}(startColor);
    
    state inputs:{=std::vector<reactor::Input<Message>*>=};
    state outputs:{=std::vector<reactor::Output<Message>*>=};
    
    input inMall:Message;
    output outMall:Message;
    
    //TODO parametrize
    output outChameneo0:Message;
    output outChameneo1:Message;
    output outChameneo2:Message;
    output outChameneo3:Message;
    output outChameneo4:Message;
    output outChameneo5:Message;
    
    //TODO parametrize
    input inChameneo0:Message;
    input inChameneo1:Message;
    input inChameneo2:Message;
    input inChameneo3:Message;
    input inChameneo4:Message;
    input inChameneo5:Message;
    
    logical action returnToMall:void;
    
    reaction(startup) {=
        
        // organize inputs and outputs in vectors for easier access
        
        //TODO parametrize
        inputs.reserve(numChameneos);
        inputs.push_back(&inChameneo0);
        inputs.push_back(&inChameneo1);
        inputs.push_back(&inChameneo2);
        inputs.push_back(&inChameneo3);
        inputs.push_back(&inChameneo4);
        inputs.push_back(&inChameneo5);
        
        //TODO parametrize
        outputs.reserve(numChameneos);
        outputs.push_back(&outChameneo0);
        outputs.push_back(&outChameneo1);
        outputs.push_back(&outChameneo2);
        outputs.push_back(&outChameneo3);
        outputs.push_back(&outChameneo4);
        outputs.push_back(&outChameneo5);
    =}
    
    //TODO parametrize
    reaction(inMall) -> returnToMall, outMall,
      outChameneo0,
      outChameneo1,
      outChameneo2,
      outChameneo3,
      outChameneo4,
      outChameneo5
    {=
        
        if(inMall.get()->type == StartMsg) {
            
            // reset local state
            meetings = 0;
            color = startColor;
            
            // start execution
            outMall.set(Message{MeetMsg, color, id});
            
        } else if(inMall.get()->type == MeetMsg) {
            
            Color otherColor = inMall.get()->color;
            int senderId = inMall.get()->id;
            color = complement(color, otherColor);
            meetings += 1;
            outputs[senderId]->set(Message{ChangeMsg, color, id});
            outMall.set(Message{MeetMsg, color, id});
            
        } else if(inMall.get()->type == ExitMsg) {
            
            color = FADED;
            outMall.set(Message{MeetingCountMsg, color, meetings});
            
        }
    =}
    
    //TODO parametrize
    reaction(
        inChameneo0,
        inChameneo1,
        inChameneo2,
        inChameneo3,
        inChameneo4,
        inChameneo5
    ) -> outMall {=
        
        Message message;
        
        // find message
        for(int i = 0; i < inputs.size(); i++) {
            if(inputs[i]->is_present()) {
                message = *(inputs[i]->get());
            }
        }
        
        if(message.type == ChangeMsg) {
            color = message.color;
            meetings += 1;
            outMall.set(Message{MeetMsg, color, id}); // go back to mall
        }
        
    =}
    
    private preamble {=
        
        Color complement(const Color color, const Color otherColor) {
            switch(color) {
              case RED:
                switch(otherColor) {
                  case RED:
                    return RED;
                  case YELLOW:
                    return BLUE;
                  case BLUE:
                    return YELLOW;
                  case FADED:
                    return FADED;
                }
              case YELLOW:
                switch(otherColor) {
                  case RED:
                    return BLUE;
                  case YELLOW:
                    return YELLOW;
                  case BLUE:
                    return RED;
                  case FADED:
                    return FADED;
                }
              case BLUE:
                switch(otherColor) {
                  case RED:
                    return YELLOW;
                  case YELLOW:
                    return RED;
                  case BLUE:
                    return BLUE;
                  case FADED:
                    return FADED;
                }
              case FADED:
                return FADED;
            }
            
           reactor::log::Error() <<
            "Error while calculating the color complement given the two colors " <<
            color << ", " << otherColor << ".";
           return FADED;
        }
        
    =}
}



main reactor ChameneosBenchmark(numIterations:int(12), numMeetings:int(200000)) {
    
    mall = new ChameneosMallReactor(numMeetings=numMeetings, numChameneos=6);
    runner = new BenchmarkRunner(numIterations=numIterations);
    
    runner.outIterationStart -> mall.inStart;
    mall.outFinished -> runner.inIterationFinish;
    
    reaction(startup) -> runner.inStart {=
        printBenchmarkInfo("ChameneosReactorLFCppBenchmark");
        printArgs("numIterations", numIterations, "numMeetings", numMeetings);
        printSystemInfo();
        runner.inStart.set();
    =}
    
    chameneo0 = new ChameneosChameneoReactor(startColor={=static_cast<Color>(0%3)=}, id=0, numChameneos=6);
    chameneo1 = new ChameneosChameneoReactor(startColor={=static_cast<Color>(1%3)=}, id=1, numChameneos=6);
    chameneo2 = new ChameneosChameneoReactor(startColor={=static_cast<Color>(2%3)=}, id=2, numChameneos=6);
    chameneo3 = new ChameneosChameneoReactor(startColor={=static_cast<Color>(3%3)=}, id=3, numChameneos=6);
    chameneo4 = new ChameneosChameneoReactor(startColor={=static_cast<Color>(4%3)=}, id=4, numChameneos=6);
    chameneo5 = new ChameneosChameneoReactor(startColor={=static_cast<Color>(5%3)=}, id=5, numChameneos=6);
    
    // Connect chamaneos and the mall.
    
    chameneo0.outMall -> mall.inChameneo0;
    chameneo1.outMall -> mall.inChameneo1;
    chameneo2.outMall -> mall.inChameneo2;
    chameneo3.outMall -> mall.inChameneo3;
    chameneo4.outMall -> mall.inChameneo4;
    chameneo5.outMall -> mall.inChameneo5;
    
    mall.outChameneo0 -> chameneo0.inMall;
    mall.outChameneo1 -> chameneo1.inMall;
    mall.outChameneo2 -> chameneo2.inMall;
    mall.outChameneo3 -> chameneo3.inMall;
    mall.outChameneo4 -> chameneo4.inMall;
    mall.outChameneo5 -> chameneo5.inMall;
    
    // Connect every Chameneo with every Chameneo.
    
    chameneo0.outChameneo0 -> chameneo0.inChameneo0;
    chameneo0.outChameneo1 -> chameneo1.inChameneo0;
    chameneo0.outChameneo2 -> chameneo2.inChameneo0;
    chameneo0.outChameneo3 -> chameneo3.inChameneo0;
    chameneo0.outChameneo4 -> chameneo4.inChameneo0;
    chameneo0.outChameneo5 -> chameneo5.inChameneo0;
    
    chameneo1.outChameneo0 -> chameneo0.inChameneo1;
    chameneo1.outChameneo1 -> chameneo1.inChameneo1;
    chameneo1.outChameneo2 -> chameneo2.inChameneo1;
    chameneo1.outChameneo3 -> chameneo3.inChameneo1;
    chameneo1.outChameneo4 -> chameneo4.inChameneo1;
    chameneo1.outChameneo5 -> chameneo5.inChameneo1;
    
    chameneo2.outChameneo0 -> chameneo0.inChameneo2;
    chameneo2.outChameneo1 -> chameneo1.inChameneo2;
    chameneo2.outChameneo2 -> chameneo2.inChameneo2;
    chameneo2.outChameneo3 -> chameneo3.inChameneo2;
    chameneo2.outChameneo4 -> chameneo4.inChameneo2;
    chameneo2.outChameneo5 -> chameneo5.inChameneo2;
    
    chameneo3.outChameneo0 -> chameneo0.inChameneo3;
    chameneo3.outChameneo1 -> chameneo1.inChameneo3;
    chameneo3.outChameneo2 -> chameneo2.inChameneo3;
    chameneo3.outChameneo3 -> chameneo3.inChameneo3;
    chameneo3.outChameneo4 -> chameneo4.inChameneo3;
    chameneo3.outChameneo5 -> chameneo5.inChameneo3;
    
    chameneo4.outChameneo0 -> chameneo0.inChameneo4;
    chameneo4.outChameneo1 -> chameneo1.inChameneo4;
    chameneo4.outChameneo2 -> chameneo2.inChameneo4;
    chameneo4.outChameneo3 -> chameneo3.inChameneo4;
    chameneo4.outChameneo4 -> chameneo4.inChameneo4;
    chameneo4.outChameneo5 -> chameneo5.inChameneo4;
    
    chameneo5.outChameneo0 -> chameneo0.inChameneo5;
    chameneo5.outChameneo1 -> chameneo1.inChameneo5;
    chameneo5.outChameneo2 -> chameneo2.inChameneo5;
    chameneo5.outChameneo3 -> chameneo3.inChameneo5;
    chameneo5.outChameneo4 -> chameneo4.inChameneo5;
    chameneo5.outChameneo5 -> chameneo5.inChameneo5;
}