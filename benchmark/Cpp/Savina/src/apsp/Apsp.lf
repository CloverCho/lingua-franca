/**
 * Parallelism benchmark from the Savina benchmark suite.
 * See https://shamsimam.github.io/papers/2014-agere-savina.pdf.
 * 
 * @author Hannes Klein
 * @author Christian Menard
 */

target Cpp {
    build-type : RelWithDebInfo,
    cmake-include: "../IncludeHeaders.cmake"
};

import BenchmarkRunner from "../BenchmarkRunner.lf";


public preamble {=
    using MatrixOfLong = std::vector<std::vector<long>>;
    using DataPtr = reactor::ImmutableValuePtr<MatrixOfLong>;
    using MutDataPtr = reactor::MutableValuePtr<MatrixOfLong>;
=}

/*
 * This benchmark performs a matrix computation. It splits a larger graphSize x graphSize matrix 
 * into smaller blocks of size blockSize x blockSize. Each of the worker reactors 
 * (ApspFloydWarshallBlock) processes one of those blocks. Thereby, the worker reactors are 
 * organized also in a matrix, replicating the structure of the blocks. Given 
 * dimension = graphSize / blocksize, there are dimension x dimension workers. Each of the workers
 * utilizes for its internal computation the data of its neighbors, which are all reactors in the 
 * same row and in the same column. After performing the internal computation, each reactor sends
 * its data to the neighbors.
 */
reactor ApspFloydWarshallBlock(
    bank_index: size_t{0},
    row_index: size_t{0},
    graphSize: size_t{300},
    blockSize: size_t{50},
    dimension: size_t{6},
    verbose: bool{false})
{
    state numNeighbors: {=const size_t=}{{=2 * (dimension - 1)=}};
    state rowOffset: {=const size_t=}{{=row_index * blockSize=}}; // row offset of the block of this reactor
    state colOffset: {=const size_t=}{{=bank_index * blockSize=}}; // column offset of the block of this reactor

    state k: size_t{0}; // iteration counter
    state reportedFinish: bool {false};

    // Extract the data block that is relevant for this instance from the given matrix 
    const method extractDataFromMatrix(
        srcData: {=const reactor::ImmutableValuePtr<MatrixOfLong>&=}
    ): {=reactor::ImmutableValuePtr<MatrixOfLong>=} {=
        auto localData = reactor::make_mutable_value<MatrixOfLong>(std::vector(blockSize, std::vector<long>(blockSize, 0)));
        
        // FIXME memcpy should be much faster here, but on the other hand this is only done during initialization
        for(size_t i = 0; i < blockSize; i++) {
            for(size_t j = 0; j < blockSize; j++) {
                (*localData)[i][j] = (*srcData)[rowOffset + i][colOffset + j];
            }
        }
        return reactor::ImmutableValuePtr<MatrixOfLong>(std::move(localData));
    =}
    
    // retrieve the element at the given position from the known data
    const method getElementAt(
        row: size_t,
        col: size_t,
        rowPorts: {=const std::vector<reactor::Input<MatrixOfLong>>&=},
        colPorts: {=const std::vector<reactor::Input<MatrixOfLong>>&=}
    ): long {=

        size_t destRow = row / blockSize;
        size_t destCol = col / blockSize;
        
        size_t localRow = row % blockSize;
        size_t localCol = col % blockSize;
        
        if (destRow == row_index) {
            const auto& data = *rowPorts[destCol].get();
            return data[localRow][localCol];
        } else if (destCol == bank_index) {
            const auto& data = *colPorts[destRow].get();
            return data[localRow][localCol];
        } else {
            std::cerr << "Error: unexpected target location (" << destCol << ',' << destRow << ")\n";
            exit(2);
        }
	=}

	input matrix: MatrixOfLong;

    input[dimension] fromRow: MatrixOfLong;
    input[dimension] fromCol: MatrixOfLong;

    output toNeighbors: MatrixOfLong;    
    output finished: void;
    
    logical action notifyNeighbors: MatrixOfLong;
    
    reaction(matrix) -> notifyNeighbors {=
        // reset local state
        k = 0;
        reportedFinish = false;
        
        // start execution
        notifyNeighbors.schedule(std::move(extractDataFromMatrix(matrix.get())));
    =}
    
    reaction(notifyNeighbors) -> toNeighbors {=
        //notify all neighbors
        toNeighbors.set(notifyNeighbors.get());
    =}
    
    reaction(fromRow, fromCol) -> notifyNeighbors, finished {=
        // do nothing if complete
        if(k == graphSize) {
            return;
        }
        
        // perform computation
        auto matrix = reactor::make_mutable_value<MatrixOfLong>(blockSize, std::vector<long>(blockSize, 0));
        
        for(size_t i = 0; i < blockSize; i++) {
            for(size_t j = 0; j < blockSize; j++) {
                int gi = rowOffset + i;
                int gj = colOffset + j;
                
                long result = getElementAt(gi, k, fromRow, fromCol) + getElementAt(k, gj, fromRow, fromCol);
                (*matrix)[i][j] = std::min(getElementAt(gi, gj, fromRow, fromCol), result);
            }
        }
       
       // increment iteration count
       k++;
       
       if(k == graphSize) {
			if(verbose && bank_index == 0 && row_index == 0) {
                // debugging and result checking
         		for(auto vec: *matrix) {
            		for(auto lon: vec) {
                		std::cout << lon << ' ';
                	}
                	std::cout << '\n';
                }
            }
            finished.set();
        }
       
        // send the result to all neighbors in the next iteration
        notifyNeighbors.schedule(std::move(matrix));        
    =}
}


reactor ApspRow(
    bank_index: size_t{0},
    blockSize: size_t{50},
    numNodes: size_t{300},
    dimension: size_t{6},
    verbose: bool{false}) {
        
    input matrix: MatrixOfLong;
    output[dimension] finished: void;

    input[{=dimension*dimension=}] fromCol: MatrixOfLong;
    output[dimension] toCol: MatrixOfLong;
        
    blocks = new[dimension] ApspFloydWarshallBlock(
        row_index=bank_index,
        blockSize=blockSize,
        graphSize=numNodes,
        dimension=dimension,
        verbose=verbose
    );
    
    // connect all blocks within the row
    (blocks.toNeighbors)+ -> blocks.fromRow;
    
    // block output to all column neighbours
    blocks.toNeighbors -> toCol;
    // block input from all column neighbours
    fromCol -x-> blocks.fromCol;
    
    // broadcast the incoming matrix to all blocks
    (matrix)+ -> blocks.matrix;
    // collect and forward finished signals from all blocks
    blocks.finished -> finished;
}

reactor ApspMatrix(
    blockSize: size_t{50},
    numNodes: size_t{300},
    dimension: size_t{6},
    verbose: bool{false})
{
	input matrix: MatrixOfLong;
	output[{=dimension*dimension=}] finished: void;
	
	rows = new[dimension] ApspRow(blockSize=blockSize, numNodes=numNodes, dimension=dimension, verbose=verbose);
	
	// broadcast the incoming matrix to all rows
    (matrix)+ -> rows.matrix;
    // collect and forward finished signals from all blocks
    rows.finished -> finished;
    
    (rows.toCol)+ -> rows.fromCol;
}

main reactor (
    numIterations: size_t{12},
    maxEdgeWeight: size_t{100},
    blockSize: size_t{50},
    numNodes: size_t{300},
    verbose: bool{false}) {
	
	state graphData: {=reactor::ImmutableValuePtr<MatrixOfLong>=};
    state numBlocksFinished: size_t{0};
	
	runner = new BenchmarkRunner(numIterations=numIterations, useInit=true, useCleanupIteration=true);
	matrix = new ApspMatrix(
        blockSize=blockSize,
        numNodes=numNodes,
        dimension={=numNodes / blockSize=},
        verbose=verbose
    );
	  
    reaction(startup) -> runner.inStart {=
        printBenchmarkInfo("ApspBenchmark");
        printArgs("numIterations", numIterations, "maxEdgeWeight", maxEdgeWeight, "numNodes", numNodes, "blockSize", blockSize);
        printSystemInfo();
        runner.inStart.set();
    =}
    
    reaction(runner.outInitializeStart) -> runner.inInitializeFinish {=
        // initialization of graph is not measured
        graphData = reactor::ImmutableValuePtr<MatrixOfLong>(generateGraph(numNodes, maxEdgeWeight));
        runner.inInitializeFinish.set();
    =}
    
    reaction(runner.outCleanupIterationStart) -> runner.inCleanupIterationFinish {=
        //TODO check if result is correct
        
        // initialization of the graph for the next iteration
        graphData = reactor::ImmutableValuePtr<MatrixOfLong>(generateGraph(numNodes, maxEdgeWeight));
        runner.inCleanupIterationFinish.set();
    =}

	// FIXME This should not produce an unused variable warning
    reaction(runner.outIterationStart) -> matrix.matrix {=
        // reset local state
        numBlocksFinished = 0;

        // start execution
        matrix.matrix.set(graphData);
	=}

	reaction (matrix.finished) -> runner.inIterationFinish {=
        for (auto& f : matrix.finished) {
            if (f.is_present()) {
                numBlocksFinished++;
            }
        }
        size_t dimension = numNodes / blockSize;
        if (numBlocksFinished == dimension*dimension) {
            runner.inIterationFinish.set();
        }
    =}

    private preamble {=  
        #include "PseudoRandom.hh"
        
        reactor::MutableValuePtr<MatrixOfLong> generateGraph(int N, int W) {
            PseudoRandom random(N);
            reactor::MutableValuePtr<MatrixOfLong> localData =
                reactor::make_mutable_value<MatrixOfLong>(std::vector(N, std::vector<long>(N, 0)));
            //MatrixOfLong localData(N, std::vector<long>(N, 0));
            
            for(int i = 0; i < N; i++) {
                for(int j = i+1; j < N; j++) {
                    int r = random.nextInt(W)+1;
                    (*localData)[i][j] = r;
                    (*localData)[j][i] = r;
                }
            }
            
            return localData;
        }
    =}
}