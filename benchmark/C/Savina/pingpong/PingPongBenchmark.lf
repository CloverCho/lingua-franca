/**
 * Basic benchmark from the Savina benchmark suite that is
 * intended to measure message-passing overhead.
 * This is based on https://www.scala-lang.org/old/node/54
 * See https://shamsimam.github.io/papers/2014-agere-savina.pdf.
 * 
 * Ping introduces a microstep delay using a logical action
 * to break the causality loop.
 * 
 * To get a sense, some (informal) results for 1,000,000 ping-pongs
 * on my PC with fast forward enabled:
 * 
 * Unthreaded: 215 ms
 * Threaded: 255 ms
 * 
 * There is no parallelism in this application, so it does not benefit from being
 * being threaded, just some additional overhead.
 * 
 * For comparison some informal results for 1,000,000 ping-pongs
 * on the same PC with the Savina Akka implementation:
 * 
 * Threaded: 1054 ms
 * 
 * @author Hannes Klein
 * @author Shaokai Lin
 */

target C;

import BenchmarkRunner from "../BenchmarkRunner.lf";

reactor Ping(count:unsigned(1000000)) {
    
    state pings_left:unsigned(0);
    
    input inStart:bool;
    output outFinished:bool;
    
    output outPing:bool;
    input inPong:bool;

    logical action serve;
    
    reaction(inStart) -> serve {=
        
        // reset local state
        self->pings_left = self->count;
        
        // start execution
        schedule(serve, 0);
    =}
    
    reaction(serve) -> outPing {=
        self->pings_left -= 1;
        SET(outPing, true); 
    =}
    
    reaction (inPong) -> serve, outFinished {=
        if (self->pings_left == 0) {
            SET(outFinished, true);
        } else {
            schedule(serve, 0);
        }
    =}
}


reactor Pong {
    
    input inPing:bool;
    output outPong:bool; 
    
    reaction(inPing) -> outPong {=
        SET(outPong, true);   
    =}
}


main reactor PingPong(numIterations:int(12), count:unsigned(1000000)) {
    
    ping = new Ping(count=count);
    runner = new BenchmarkRunner(numIterations=numIterations);
    
    runner.outIterationStart -> ping.inStart;
    ping.outFinished -> runner.inIterationFinish;
    
    reaction(startup) -> runner.inStart {=
        printf("PingPongBenchmark\n");
        printf("numIterations: %d, count: %d\n", self->numIterations, self->count);
        printSystemInfo(); 
        SET(runner.inStart, true);
    =}
    
    pong = new Pong();
    
    ping.outPing -> pong.inPing;
    pong.outPong -> ping.inPong;
}