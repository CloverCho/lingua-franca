// Source has an array as a parameter, the elements of which it passes
// to Print. Note that size of the array cannot change at runtime.
target Python;
reactor Source(sequence:int[](0, 1, 2, 4)) {
    output out:int;
    state count:int(0);
    logical action next;
    reaction(startup, next) -> out, next {=
        SET(out, sequence[self.count])
        self.count+=1
        if self.count < len(sequence):
            schedule(next, 0)
    =}
}
reactor Print {
    input _in:int;
    state count:int(1);
    state received:int(0);
    reaction(_in) {=
        self.received+=1
        print("Received: {:d}\n".format(_in.value))
        if _in.value != self.count:
            sys.stderr.write("ERROR: Expected {:d}.\n".format(self.count))
            exit(1)
        self.count+=1
    =}
    reaction(shutdown) {=
        if self.received == 0:
            sys.stderr.write("ERROR: Final reactor received no data.\n")
            exit(3)
    =}
}
main reactor ArrayAsParameter {
    s = new Source(sequence = (1, 2, 3, 4));
    p = new Print();
    s.out -> p._in;
}