// Test multiport to multiport connections.
// See also MultiportToMultiport.
target Python;

reactor Source(width(2)) {
    output[width] out;
    reaction (startup) -> out {=
        for i in range(len(out)):
            out[i].set(i)
    =}
}

reactor Destination(width(2)) {
    input[width] _in;
    reaction (_in) {=
        for i in range(len(_in)):
            if _in[i].is_present:
                print("Received on channel {:d}: ".format(i), _in[i].value)
                # NOTE: For testing purposes, this assumes the specific
                # widths instantiated below.
                if _in[i].value != i % 3:
                    sys.stderr.write("ERROR: expected {:d}!\n".format(i % 3))
                    exit(1)
        if get_elapsed_logical_time() != SEC(1):
            sys.stderr.write("ERROR: Expected to receive input after one second.\n")
            exit(2)
    =}
}

main reactor MultiportToMultiport2After {
    a1 = new Source(width = 3);
    a2 = new Source(width = 2);
    b = new Destination(width = 5);
    a1.out, a2.out -> b._in after 1 sec;
}