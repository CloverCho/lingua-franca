// Test bank of reactors to multiport input with id parameter in the bank.
target Python;

reactor Source() {
    output out;
    
    reaction (startup) -> out {=
        out.set(self.bank_index)
    =}
}

reactor Sink(width(4)) {
    input[width] _in;
    state received(false);
    
    reaction (_in) {=
        for i in range(len(_in)):
            if _in[i].is_present is True:
                print("Received on channel {:d}: {:d}\n".format(i, _in[i].value))
                self.received = True
                if _in[i].value != i:
                    sys.stderr.write("ERROR: expected {:d}\n".format(i))
                    exit(1)
    =}
    reaction(shutdown) {=
        if self.received is False:
            sys.stderr.write("ERROR: Sink received no data\n")
            exit(1)
    =}
}
main reactor BankToMultiport(width(5)) {
    source = new[width] Source();
    sink = new Sink(width = width);
    source.out -> sink._in;
}