 // Check multiport output to bank of recipients.
target Python {
    timeout: 2 sec,
    threads: 4,
    fast: true
}; 
reactor Source {
    output[3] out;
    reaction(startup) -> out {=
        for i in range(len(out)):
            out[i].set(i)
    =}
}
reactor Destination {
    input _in;
    state received(0);
    reaction(_in) {=
        print("Destination " + str(self.bank_index) + " received " + str(_in.value))
        if self.bank_index != _in.value:
            sys.stderr.write("ERROR: Expected " + str(self.bank_index))
            exit(1)
        self.received = True
    =}
    reaction(shutdown) {=
        if self.received is not True:
            sys.stderr.write("ERROR: Destination " + str(self.bank_index) + " received no input!\n")
            exit(1)
        print("Success.")
    =}
}

main reactor MultiportToBank { 
    a = new Source();
    b = new[3] Destination();
    a.out -> b._in;
}