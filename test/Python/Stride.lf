// This example illustrates state variables and parameters on the wiki.
// For this test, success is just compiling and running.
target Python {
    timeout: 2 sec,
    fast: true
};
reactor Count(stride:int(1)) {
    state count:int(1);
    output y:int;
    timer t(0, 100 msec);
    reaction(t) -> y {=
        y.set(self.count)
        self.count += self.stride
    =}
}
reactor Display {
    input x:int;
    state expected:int(1); // for testing.
    reaction(x) {=
        print("Received: ", x.value)
        if x.value != self.expected:
            sys.stderr.write("ERROR: Expected {:d}.\n".format(self.expected))
        self.expected += 2
    =}
}
main reactor Stride {
    c = new Count(stride = 2);
    d = new Display();
    c.y -> d.x;
}