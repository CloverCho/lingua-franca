 // Check multiport output to bank of recipients.
 // Here, the bank is smaller than the width of the sending port.
target C {
    timeout: 2 sec,
    threads: 4,
    fast: true
}; 
reactor Source {
    output[3] out:int;
    reaction(startup) -> out {=
        for(int i = 0; i < out_width; i++) {
            SET(out[i], i);
        }
    =}
}
reactor Destination(instance:int(0)) {
    input in:int;
    state received:bool(false);
    reaction(in) {=
        printf("Destination %d received %d.\n", self->instance, in->value);
        if (self->instance != in->value) {
            printf("ERROR: Expected %d.\n", self->instance);
            exit(1);
        }
        self->received = true;
    =}
    reaction(shutdown) {=
        if (!self->received) {
            fprintf(stderr, "ERROR: Destination %d received no input!\n", self->instance);
            exit(1);
        }
        printf("Success.\n");
    =}
}
reactor Container {
    input[3] in:int;
    c = new[3] Destination();
    in -> c.in;
}

main reactor MultiportToBankHierarchy { 
    a = new Source();
    b = new Container();
    a.out -> b.in;
}