 // Check bank of reactors sending to bank of reactors.
target C {
    timeout: 2 sec,
    threads: 4
}; 
reactor Source {
	timer t(0, 200 msec);
	output out:int;
	state s:int(0);
	reaction(t) -> out {=
        SET(out, self->s);
   	    self->s++;
	=}
}
reactor Destination {
	state s:int(0);
	input in:int;
	reaction(in) {=
		printf("Received: %d.\n", in->value);
		if (in->value != self->s) {
			printf("ERROR: Expected %d.\n", self->s);
			exit(1);
		}
		self->s += 1;
	=}
	reaction(shutdown) {=
        if (self->s == 0) {
            fprintf(stderr, "ERROR: Destination received no input!\n");
            exit(1);
        }
        printf("Success.\n");
    =}
}

main reactor BankToBank { 
	a = new[4] Source();
	b = new[4] Destination();
	a.out -> b.in;
}