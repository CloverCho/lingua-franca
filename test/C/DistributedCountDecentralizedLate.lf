 /** 
 * Test a form of a distributed deterministic system
 * where a federate that receives timestamped messages has a timer in addition to the messages
 * as triggers. Therefore, careful coordination of the advancement of time using Ptides is needed.
 * @author Edward A. Lee
 * @author Soroush Bateni
 */
target C {
    timeout: 4900 msec,
    coordination: decentralized
};

import Count from "lib/Count.lf";

reactor Print {
    input in:int;
    state success:int(0);
    state success_tardy:int(0);
    timer t(0, 10 usec); // Force a timer to be invoke periodically
                         // to ensure logical time will advance in the
                         // absence of incoming messages.
    state c:int(0);
    reaction(in) {=
        tag_t current_tag = get_current_tag();
        printf("At tag (%lld, %u) received %d. Intended tag is (%lld, %u).\n",
            get_elapsed_logical_time(),
            get_microstep(),
            in->value,
            in->intended_tag.time - get_start_time(),
            in->intended_tag.microstep);
        if (compare_tags2(current_tag.time - get_start_time(), current_tag.microstep, 
                         SEC(1) *  self->c, 0) == 0) {
            self->success++; // Message was on-time
        }
        self->c++;
    =} tardy {=
        tag_t current_tag = get_current_tag();
        printf("Message is tardy by (%lld, %u).\n",
	            current_tag.time - in->intended_tag.time,
	            current_tag.microstep - in->intended_tag.microstep);
        self->success_tardy++;
        self->c++;
    =}
    reaction(t) {=
        // Do nothing.
    =}
    
    reaction(shutdown) {=
        if ((self->success +  self->success_tardy) != 5) { // At timeout (e.g., t = 5000000000), we allow
                                                           // for arbitrary microsteps. Therefore, we get
                                                           // 6 messages rather than 5.
            fprintf(stderr, "Failed to detect tardiness of messages.\n");
            exit(1);
        } else {
            printf("Successfully detected tardiness (%d tardy, %d on-time).\n", self->success_tardy, self->success);
        }
    =}
}

federated reactor DistributedCount {
    c = new Count();
    p = new Print();
    c.out -> p.in;    // Indicating a 'logical' connection.
}