target C {
    timeout: 2 sec,
    threads: 4
};

import TakeTime from "concurrent/Threaded.lf";

reactor Print {
    input in:int;
    state count:int(0);
    state received:int(0);
    reaction(in) {=
        self->received++;
        printf("Received: %d at logical time %lld\n", in->value, get_elapsed_logical_time());
        if (in->value != self->count + 400000000) {
            printf("ERROR: Expected %d.\n", self->count + 400000000);
            exit(1);
        }
        self->count++;
    =}
    reaction(shutdown) {=
        if (self->received == 0) {
            printf("ERROR: Final reactor received no data.\n");
            exit(3);
        }
    =}
}

main reactor Pipeline {
    timer t(0, 200 msec);
    state count:int(0);
    
    c1 = new TakeTime();
    c2 = new TakeTime();
    c3 = new TakeTime();
    c4 = new TakeTime();
    p = new Print();
    
    reaction(t) -> c1.in {=
        SET(c1.in, self->count++);        
    =}
   
    c1.out -> c2.in after 200 msec;
    c2.out -> c3.in after 200 msec;
    c3.out -> c4.in after 200 msec;
    c4.out -> p.in;
}
