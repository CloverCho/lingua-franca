// Check for speedup of multithreaded execution on multicore machines.
// Each instance of TakeTime takes 200 ms to transport the input to the output.
// Four of them are instantiated.
// Note that without parallel execution, there is no way this can keep up with real time
// since in every 200 msec cycle it has 800 msec of work to do.
// On a quad-core machine, however, it does pretty well, completing 800 msec of work
// in about 225 msec.
target C {
    timeout: 2 sec,
    threads: 4
};
reactor Source {
	timer t(0, 200 msec);
	output out:int;
	state s:int(0);
	reaction(t) -> out {=
		set(out, self->s);
		self->s++;
	=}
}
reactor TakeTime {
	input in:int;
	output out:int;
	reaction(in) -> out {=
		// struct timespec sleep_time = {(time_t) 0, (long)200000000};
		// struct timespec remaining_time;
		// nanosleep(&sleep_time, &remaining_time);
		int offset = 0;
		for (int i = 0; i < 100000000; i++) {
		    offset++;
		}
		set(out, in + offset);
	=}
}
reactor Destination {
	state s:int(400000000);
	input in1:int;
	input in2:int;
	input in3:int;
	input in4:int;
	reaction(in1, in2, in3, in4) {=
		int sum = in1 + in2 + in3 + in4;
		printf("Sum of received: %d.\n", sum);
		if (sum != self->s) {
			printf("ERROR: Expected %d.\n", self->s);
			// exit(1);
		}
		self->s += 4;
	=}
}
main reactor ThreadedThreaded {
	a = new Source();
	t1 = new TakeTime();
	t2 = new TakeTime();
	t3 = new TakeTime();
	t4 = new TakeTime();
	b = new Destination();
	a.out -> t1.in;
	a.out -> t2.in;
	a.out -> t3.in;
	a.out -> t4.in;
	t1.out -> b.in1;
	t2.out -> b.in2;
	t3.out -> b.in3;
	t4.out -> b.in4;
}