/** 
 * Tests distributed execution of modes.
 * 
 * Depends on timing of ModalActions test.
 */
target C {
    timeout: 5 sec,
    coordination: centralized
};

import Modal from "ModalActions.lf"
import TraceTesting from "util/TraceTesting.lf"

reactor Controller {
    output next:bool
    
    timer stepper(1sec, 1sec)
    
    // Trigger mode change
    reaction(stepper) -> next {=
        SET(next, true);
    =}
}

reactor SingleModalFederate {
    input next:bool
    output[5] outputs:int
    
    modal = new Modal()
    
    next -> modal.next
    
    modal.mode_switch,
    modal.action1_sched,
    modal.action1_exec,
    modal.action2_sched,
    modal.action2_exec
    -> outputs
}

reactor MergeTwo(size:int(0)) {
    input[size] left:int
    input[size] right:int
    output[size+size] merged:int
    
    reaction(left, right) -> merged {=
        for (int i = 0; i < left_width; i++) {
            if (left[i]->is_present) {
                SET(merged[i], left[i]->value);
            }
            if (right[i]->is_present) {
                SET(merged[i+left_width], right[i]->value);
            }
        }
    =}
}

federated reactor {
    control = new Controller()
    modal1 = new SingleModalFederate()
    modal2 = new SingleModalFederate()
    merger = new MergeTwo(size = 5)
    test = new TraceTesting(
        events_size = 10,
        trace_size = 378, 
        trace = (
            200000000,0,0,1,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
            500000000,0,0,0,1,1,1,0,0,0,0,0,0,1,1,0,0,0,0,0,0,
            250000000,0,0,1,1,0,1,0,0,0,0,0,0,0,1,0,0,0,0,0,0,
            250000000,1,1,0,1,0,1,1,1,0,0,0,0,0,1,1,1,0,0,0,0,
            250000000,0,1,0,1,0,1,0,1,0,0,0,0,1,1,0,1,0,0,0,0,
            250000000,0,1,0,1,0,1,0,1,1,1,1,1,0,1,0,1,1,1,0,0,
            250000000,0,1,0,1,0,1,1,1,0,1,0,1,0,1,0,1,0,1,0,0,
            250000000,1,1,0,1,0,1,0,1,0,1,0,1,0,1,0,1,0,1,1,1,
            250000000,0,1,0,1,1,1,0,1,0,1,0,1,0,1,0,1,1,1,0,1,
            250000000,0,1,1,1,0,1,0,1,0,1,1,1,0,1,0,1,0,1,0,1,
            250000000,0,1,0,1,0,1,0,1,0,1,0,1,0,1,1,1,0,1,0,1,
            250000000,1,1,0,1,1,1,1,1,0,1,0,1,1,1,0,1,0,1,0,1,
            500000000,0,1,0,1,0,1,0,1,1,1,1,1,0,1,1,1,1,1,0,1,
            250000000,0,1,0,1,0,1,1,1,0,1,0,1,0,1,0,1,0,1,0,1,
            250000000,1,1,0,1,0,1,0,1,0,1,0,1,0,1,0,1,0,1,1,1,
            250000000,0,1,1,1,0,1,0,1,0,1,0,1,0,1,0,1,1,1,0,1,
            250000000,0,1,0,1,0,1,0,1,0,1,1,1,0,1,0,1,0,1,0,1,
            250000000,0,1,0,1,1,1,0,1,0,1,0,1,1,1,0,1,0,1,0,1
        ), training = false)
    
    control.next -> modal1.next
    control.next -> modal2.next
    modal1.outputs -> merger.left after 200msec
    modal2.outputs -> merger.right after 700msec
    merger.merged -> test.events
}
