// Check multiport output to bank of recipients.
// Here, the bank is smaller than the width of the sending port.
target TypeScript {
    timeout: 2 sec
}; 
reactor Source {
    output out:number;
    reaction(startup) -> out {=
        out = this.getBankIndex();
    =}
}
reactor Container {
    output[3] out:number;
    s = new[3] Source();
    s.out -> out;
}
reactor Destination {
    input[3] inp:number;
    state received:boolean(false);
    reaction(inp) {=
        for (let i = 0; i < inp.length; i++) {
            console.log("Destination channel " + i + " received " + inp[i] + ".");
            if (i != inp[i]) {
                util.requestErrorStop("ERROR: Expected " + i + ".");
            }
        }
        received = true;
    =}
    reaction(shutdown) {=
        if (!received) {
            util.reportError("ERROR: Destination received no input!");
        }
        console.log("Success.");
    =}
}

main reactor {
    a = new Container();
    b = new Destination();
    a.out -> b.inp;
}
