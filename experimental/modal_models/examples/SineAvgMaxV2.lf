/**
 * Ptolemy II Example for Modes.
 * An SDF model where the ModalModel requires more than one token
 * on its input in order to fire.
 * http://ptolemy.org/systems/models/modal/ModalSDF/index.html
 * 
 * @author Alexander Schulz-Rosengarten
 */
target Python{
    threads: 0
};

import Plotter, Sinewave from "SineAvgMax.lf"

reactor ModalModel(sample_size(10)) {
    input data;
    output out;
    
    state sample({=[None] * sample_size=});
    state count(0);
    
    logical action process
    
    // @label Collect
    reaction(data) -> process {=
    	self.sample[self.count] = data.value
        self.count += 1
        if self.count == self.sample_size:
            self.count = 0
            process.schedule(0)
    =}
    
    initial mode AVG {

	    /** @label Process and Transition */
	    reaction(process) -> out, MAX {=
	    	#print("Processing: ", self.sample)
            out.set(sum(self.sample) / self.sample_size)
            # Transition to MAX
            set_mode(MAX)
	    =}
    
    }
    
    mode MAX {
    
	    /** @label Process and Transition */
	    reaction(process) -> out, AVG {=
	    	#print("Processing: ", self.sample)
            out.set(max(self.sample))
            # Transition to MAX
            set_mode(AVG)
    	=}
    }
}

main reactor SineAvgMaxV2 {
    s = new Sinewave(sample_rate = 125 msec, frequency = 0.44)
    m = new ModalModel()
    p = new Plotter()
    s.data -> m.data
    m.out -> p.data
}
